// "BatchConvertStacksToZProjJPGS"
//
// created by Aaron on 2011-26-10
// This macro takes all of the tifs in a folder and its subfolders
// and creates a SD Z-projection


// (Slightly) Adapted by Greg Jefferis from code at
// http://rsb.info.nih.gov/ij/macros/BatchProcessFolders.txt

// jefferis@gmail.com

// v 0.1 2007-11-07
// - first version in response to a request from Jai

requires("1.33s"); 
dir = getDirectory("Choose a stacks directory");
outputDir = getDirectory("Choose output directory");
setBatchMode(true);
count = 0;
countFiles(dir);
print("Total files: "+count);
n = 0;
processFiles(dir, outputDir);

function countFiles(dir) {
		list = getFileList(dir);
	for (i=0; i<list.length; i++) {
		if (endsWith(list[i], "/"))
            countFiles(""+dir+list[i]);
	else
		count++;
	}
}

function processFiles(dir,outputDir) {
	list = getFileList(dir);
    for (i=0; i<list.length; i++) {
        if (endsWith(list[i], "/"))
            processFiles(""+dir+list[i], outputDir);
        else {
		showProgress(n++, count);
              processFile(dir,outputDir,list[i]);
        }
    }
}

function processFile(dir,outputDir,file) {
	if (endsWith(file, ".tif")) {
		if(File.exists(outputDir + substring(file,0,lengthOf(file)-4)+".jpg")==1) {
			print("tif exist" + file);
			return 0;
		} else {
			 print(outputDir+substring(file,0,lengthOf(file)-4)+".jpg");
			 print(File.exists(outputDir+substring(file,0,lengthOf(file)-4)+".jpg"));
		}
		path = dir+list[i];
		open(path);
		processImage();
		saveAs("Jpeg", outputDir+substring(file,0,lengthOf(file)-4));
		close();
		close();
	}
	
}

function processImage() {
	run("Z Project...", "projection=[Standard Deviation]");
	run("8-bit");
	// run("Flip Vertically");
}
