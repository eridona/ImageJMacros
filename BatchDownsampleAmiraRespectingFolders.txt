// "BatchConvertStacksToZProjJPGS"
//
// This macro batch processes all the amira files 
// in a folder and any subfolders in that folder.
// It then saves them in the Amiramesh format using Fiji's AmiraMesh plugin
// The new file will have the same name
// If you give the same input and output folders nothing will happen

// (Slightly) Adapted by Greg Jefferis from code at
// http://rsb.info.nih.gov/ij/macros/BatchProcessFolders.txt

// jefferis@gmail.com

// v 0.1 2009-11-25
// - first version in response to a request from Jai

requires("1.33s"); 
dir = getDirectory("Choose a stacks directory");
outputDir = getDirectory("Choose output directory");
setBatchMode(true);
count = 0;
countFiles(dir);
print("Total files: "+count);
n = 0;
processFiles(dir, outputDir);

function countFiles(dir) {
		list = getFileList(dir);
	for (i=0; i<list.length; i++) {
		if (endsWith(list[i], "/"))
            countFiles(""+dir+list[i]);
	else
		count++;
	}
}

function processFiles(dir,outputDir) {
	list = getFileList(dir);
    for (i=0; i<list.length; i++) {
        if (endsWith(list[i], "/")){
			if(File.exists(""+outputDir+list[i]) != 1){
				File.makeDirectory(""+outputDir+list[i]);
			}
            processFiles(""+dir+list[i], ""+outputDir+list[i]);
        } else {
		showProgress(n++, count);
              processFile(dir,outputDir,list[i]);
        }
    }
}

function processFile(dir,outputDir,file) {
	if (endsWith(file,".am")) {
		newpath=outputDir+file;
		if(File.exists(newpath)){
			print("Skipping file: "+file+" since output already exists");
			return;
		}
		path = dir+file;
		open(path);
		processImage();
		run("AmiraMesh ...", "amirafile=["+newpath+"]");			
		close();
		close();
	}
}

function processImage() {
	run("Scale...", "x=0.5 y=0.5 z=1.0 interpolation=Bilinear process create title=scaledimage");
	run("8-bit");
}
